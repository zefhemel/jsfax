module cps

imports
  include/Jsfax
  lift

strategies

  js-cps-transform =
    alltd(js-cps-function)
    ; topdown(try(js-cps-statements))

  js-cps-function :
    js-stat|[ function x~(x*) { stat* }]| -> js-stat|[ function x(x*, callback) { stat2* } ]|
    where {| InAsyncFunction
           : rules ( InAsyncFunction := 1 )
           ; stat2* := <alltd(js-cps-function)> stat*
           |}

  js-cps-function :
    js-stat|[ function x(x*) { stat* }]| -> js-stat|[ function x(x*) { stat2* } ]|
    where {| InAsyncFunction
           : rules ( InAsyncFunction := 0 )
           ; stat2* := <alltd(js-cps-function)> stat*
           |}

  js-cps-function :
    js-e|[ function~(x*) { stat* }]| -> js-e|[ function(x*, callback) { stat2* } ]|
    where {| InAsyncFunction
           : rules ( InAsyncFunction := 1 )
           ; stat2* := <alltd(js-cps-function)> stat*
           |}

  js-cps-function :
    js-e|[ function(x*) { stat* }]| -> js-e|[ function(x*) { stat2* } ]|
    where {| InAsyncFunction
           : rules ( InAsyncFunction := 0 )
           ; stat2* := <alltd(js-cps-function)> stat*
           |}

  js-cps-function :
    js-stat|[ return; ]| -> js-stat|[ { callback(); return; } ]|
    where 1 := <InAsyncFunction>

  js-cps-function :
    js-stat|[ return e; ]| -> js-stat|[ { callback(e); return; } ]|
    where 1 := <InAsyncFunction>

strategies

  js-cps-statements :
    [stat|stat*] -> [stat|<js-cps-statements> stat*]
    where <js-is-sync> stat

  js-cps-statements :
    [stat|stat*] -> <js-cps-stats> [stat|stat*]
    where not(<js-is-sync> stat)

  js-cps-statements :
    [] -> []

  js-cps-stats :
    [js-stat|[ var x = e; ]||stat*] -> <js-cps-expression(|[js-stat|[ var x = result__; ]||stat*])> e

  js-cps-stats :
    [stat|stat*] -> [stat|<js-cps-statements> stat*]

strategies

  js-cps-expression(|stat*) =
    ?e
    ; if js-is-sync then
        stat2* := <js-cps-statements> stat*
        ; !js-stat*|[ var result__ = e; stat2* ]|
      else
          js-cps-exp(|stat*)
        <+ debug(!"Could not CPS this async: "); fail
      end

  js-cps-exp(|stat*) :
    js-e|[ e~(e*) ]| -> js-stat*|[e(e*, function(result__) {
      stat2*
    });]|
    with stat2* := <js-cps-statements <+ debug(!"Stats failed: "); fail> stat*
